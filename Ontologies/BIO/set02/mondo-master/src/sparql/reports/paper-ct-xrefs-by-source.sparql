PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
prefix IAO: <http://purl.obolibrary.org/obo/IAO_>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix oio: <http://www.geneontology.org/formats/oboInOwl#>
prefix def: <http://purl.obolibrary.org/obo/IAO_0000115>
prefix owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

# number of cross references in Mondo by prefix

SELECT ?prefix (COUNT(DISTINCT ?value) AS ?ct_xrefs)

WHERE 
{
  VALUES ?mapping { oboInOwl:hasDbXref }
  ?cls a owl:Class; 
     	?mapping ?value .
    	
  	?xref_anno2 a owl:Axiom ;
           owl:annotatedSource ?cls ;
           owl:annotatedProperty ?mapping ;
           owl:annotatedTarget ?value ;
           oboInOwl:source ?source .

  FILTER (str(?source)="MONDO:equivalentTo")
  
  FILTER NOT EXISTS {
  	?cls owl:deprecated ?deprecated .
  }

  FILTER( !isBlank(?cls) && STRSTARTS(str(?cls), "http://purl.obolibrary.org/obo/MONDO_"))
  BIND( STRBEFORE(str(?value),":") AS ?prefix)
} GROUP BY ?prefix ORDER BY DESC(?ct_xrefs)

