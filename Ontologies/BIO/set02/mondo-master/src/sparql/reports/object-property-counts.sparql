prefix obo: <http://purl.obolibrary.org/obo/> 
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?p ?label (count(?res) as ?count)
WHERE {
  {
  ?entity rdfs:subClassOf ?res .
   ?res a owl:Restriction ;
    owl:onProperty ?p ;
    owl:someValuesFrom ?y  .
  ?entity rdf:type owl:Class .
  OPTIONAL {
  ?p rdfs:label ?label .
    } 
      FILTER (isIRI(?entity) && STRSTARTS(str(?entity), "http://purl.obolibrary.org/obo/MONDO_"))

  } UNION {
    ?entity owl:equivalentClass ?intersection .
  	?intersection owl:intersectionOf ?lst .
  	?lst rdf:rest*/rdf:first ?res .
  	?res owl:onProperty ?p .
  	OPTIONAL {
  		?p rdfs:label ?label .
    } 
    FILTER (isIRI(?entity) && STRSTARTS(str(?entity), "http://purl.obolibrary.org/obo/MONDO_"))


  }
  
} GROUP BY ?p ?label
