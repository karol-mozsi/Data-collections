prefix owl: <http://www.w3.org/2002/07/owl#>
prefix oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?term ?term_label ?related
WHERE 
{ 
  { 
    
    ?term oboInOwl:hasRelatedSynonym ?related ;
      oboInOwl:hasExactSynonym ?exact ;
      a owl:Class .
    
    ?term rdfs:label ?term_label .
    
    FILTER NOT EXISTS {
      ?a owl:annotatedSource ?term ;
         owl:annotatedProperty oboInOwl:hasExactSynonym ;
         owl:annotatedTarget ?exact_t ;
         oboInOwl:hasDbXref ?xref .
         FILTER (str(?exact_t)=str(?exact))
         FILTER (regex(str(?xref), "(NCIT|GARD|Orphanet)"))
    }
    
    FILTER (str(?related)=str(?exact))
    FILTER (str(?term_label)!=str(?exact))
    FILTER (isIRI(?term) && regex(str(?term), "^http://purl.obolibrary.org/obo/MONDO_"))
  }
}
