prefix oio: <http://www.geneontology.org/formats/oboInOwl#>
prefix owl: <http://www.w3.org/2002/07/owl#>
prefix obo: <http://purl.obolibrary.org/obo/>

SELECT ?cls ?type ?xref ?prefix ?deprecated WHERE 
{
  ?cls a owl:Class ; 
     oio:hasDbXref ?xref .
  ?a owl:annotatedSource ?cls ;
     owl:annotatedProperty oio:hasDbXref ;
     owl:annotatedTarget ?xref ;
     oio:source ?type
  OPTIONAL { ?cls owl:deprecated ?deprecated . }
  FILTER (regex(str(?type), "MONDO:(equivalentTo|subClassOf)"))
  FILTER (!isBlank(?cls))
  FILTER ( isIRI(?cls) && regex(str(?cls), "^http://purl.obolibrary.org/obo/MONDO_"))
  BIND( strbefore(str(?xref), ":") AS ?prefix)
}
