PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX replaced_by: <http://purl.obolibrary.org/obo/IAO_0100001>

# This querry report all the Mondo terms that has "congenital" in their label and exclude the terms that have "has modifier some congenital" as SUBCLASS axiom (ie, not logical definition!).

SELECT distinct ?cls ?label 
WHERE {
  {
	?cls a owl:Class; 
  		rdfs:label ?label .
    
  	FILTER(regex(str(?label),"congenital"))
        
    FILTER NOT EXISTS {
      ?cls rdfs:subClassOf ?res .
      ?res a owl:Restriction ;
    	owl:onProperty <http://purl.obolibrary.org/obo/RO_0002573> ;
    	owl:someValuesFrom <http://purl.obolibrary.org/obo/MONDO_0021140>  .
    }
  
    
    FILTER NOT EXISTS {
    	?cls owl:deprecated "true"^^xsd:boolean
  	}
    FILTER( !isBlank(?cls) && regex(str(?cls), "^http://purl.obolibrary.org/obo/MONDO_"))
  }
  	
} 
